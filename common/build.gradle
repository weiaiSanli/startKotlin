apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

//必须引入这2个插件
apply plugin : 'com.github.dcendents.android-maven'
apply plugin : 'com.jfrog.bintray'

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
        arg("AROUTER_GENERATE_DOC", "enable")
    }
}
android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion



        buildConfigField("boolean", "isDebug", "true")
        multiDexEnabled true
    }



    buildTypes {
        tesing {// 测试
            minifyEnabled false
            buildConfigField("boolean", "isDebug", "true")
        }
        demo {// 预上线
            minifyEnabled false
            buildConfigField("boolean", "isDebug", "true")
        }
        release {// 正式
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            buildConfigField("boolean", "isDebug", "false")

        }
    }

}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api "com.android.support:appcompat-v7:$support_library_version"
    api "com.android.support:recyclerview-v7:$support_library_version"
    api "com.android.support:cardview-v7:$support_library_version"

    api 'com.android.support.constraint:constraint-layout:1.1.3'

    api 'com.squareup.retrofit2:retrofit:2.6.0'
    //Retrofit内置RxJava
    api 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'

    //Rxjava2
    api 'io.reactivex.rxjava2:rxjava:2.0.1'
    api 'io.reactivex.rxjava2:rxandroid:2.0.1'

    //Retrofit配套
    api 'com.squareup.retrofit2:converter-scalars:2.6.0'

    //fastJson转化器
    api 'com.alibaba:fastjson:1.1.63.android'

    //okhttp
    api 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    //kotlin的协程库  1:DSL使用  2:Dispatchers.Main 使用
    api("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0-M1")
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0-M1'

    //阿里的Arouter
    api 'com.alibaba:arouter-api:1.5.0'
    kapt "com.alibaba:arouter-compiler:$rootProject.ext.arouter_version"

    //fragment加载框架
    api 'me.yokeyword:fragmentation:1.3.7'
    //工具类
    api 'com.blankj:utilcode:1.24.2'

    //progress的绘制类
    api 'com.wang.avi:library:1.0.1'
    api 'com.android.support:multidex:1.0.3'
    api "com.android.support:recyclerview-v7:$support_library_version"

}

// This is the library version used when deploying the artifact
group = "com.github.shiqiang"
version = "1.2.1" //PROJ_VERSION 替换成自己的的版本号

def siteUrl =  'https://gitlab.com/weiaiSanli/startkotlin'
def gitUrl = 'https://gitlab.com/weiaiSanli/startkotlin.git'

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                description "common项目基类" //替换成自己的工程描述

                // Add your description here
                name "sqcommonlibrary"  //替换成自己的工程名称
                url siteUrl // 项目主页，这里是引用上面定义好,替换成自己的工程网络地址，例如github中发布的源码地址

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'//替换证书类型，例如'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'//替换证书地址，例如'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id "shiq" //替换成自己的id
                        name "shiq" //替换成自己的名字
                        email "shiqiang_xd0529@foxmail.com" //替换成自己的邮箱
                    }
                }
                scm {
//                    connection PROJ_VCSURL //替换成自己的Git仓库地址。
//                    developerConnection PROJ_VCSURL //替换成自己的Git仓库地址。
//                    url PROJ_WEBSITEURL //替换成自己的项目主页。

                    connection gitUrl // Git仓库地址。
                    developerConnection gitUrl // Git仓库地址。
                    url siteUrl // 项目主页。

                }
                dependencies{
                    dependency{
                        groupId "com.gitlab.shiqiang"
                        version "1.2.1"
                        artifactId "sqcommonlibrary"
                        scope "compile"
                    }
                }
            }
        }
    }
}



// 生成jar包的task，不需要修改。
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
// 生成jarDoc的task，不需要修改。
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // destinationDir = file("../javadoc/")
    failOnError false // 忽略注释语法错误，如果用jdk1.8你的注释写的不规范就编译不过。
}
// 生成javaDoc的jar，不需要修改。
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties();
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def bintray_user = properties.getProperty("bintray.user")
def bintray_key = properties.getProperty("bintray.apikey")
//def bintray_gpg_pwd = properties.getProperty("bintray.gpg.password")?

bintray {
    user = bintray_user
    key = bintray_key
    configurations = ['archives']

    pkg {
        repo = "sqcommon"
        name = "sqcommonlibrary"
//        userOrg = "angryboby"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version{
            gpg{
                sign = true
//                passphrase = bintray_gpg_pwd
            }
        }
    }
}

javadoc {
    options{
        //如果你的项目里面有中文注释的话，必须将格式设置为UTF-8，不然会出现乱码
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
    }
}